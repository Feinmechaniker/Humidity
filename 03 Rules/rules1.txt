on System#Boot do
 GPIO,14,0                              // D5 OFF (Lüfter)
 TaskValueSet,Fan,State,2               // default auf Automatik
 TaskValueSet,SW,Temp,10                // default Temperatur
 LoopTimerSet,1,10                      // Start loop timer 1, 2 sec interval
endon

on Rules#Timer=1 do
 // Messdaten erfassen
 Let,1,[FAussen#Humidity]               // relative Feuchte außen in % 
 Let,2,[FInnen#Humidity]                // relative Feuchte innen in %
 Let,3,[FAussen#Temperature]            // Temperatur außen
 Let,4,[FInnen#Temperature]             // Temperatur innen
 
 // absolute Feuchte berechnen
 Let,5,(%v1%*13.2434/(273.15+%v3%)*exp((17.62*%v3%)/(243.5+%v3%))) // absolute Feuchte außen
 Let,6,(%v2%*13.2434/(273.15+%v4%)*exp((17.62*%v4%)/(243.5+%v4%))) // absolute Feuchte innen
 Let,7,(%v6%-%v5%)                      // Differenz der absoluten Feuchte innen - außen
 
 // anzeigen
 TaskValueSet,AFeuchte,Aussen,(%v5%)   // absolut außen anzeigen
 TaskValueSet,AFeuchte,Innen,(%v6%)    // absolut innen anzeigen
 // TaskValueSet,AFeuchte,Aussen,(%v1%*13.2434/(273.15+%v3%)*exp((17.62*%v3%)/(243.5+%v3%)))
 // TaskValueSet,AFeuchte,Innen, (%v2%*13.2434/(273.15+%v4%)*exp((17.62*%v4%)/(243.5+%v4%)))
 TaskValueSet,AFeuchte,Delta,(%v7%)    // Differenz anzeigen

 // Wenn Feuchteschwellwert in g/m³ überschritten UND Außentemperatur >Temperatur UND Automatik
 if [AFeuchte#Delta]>=0.1 and [FAussen#Temperature]>=[SW#Temp] and [Fan#State]=2    
   GPIO,14,1                           // Lüfter einschalten
    else
     GPIO,14,0                         // Lüfter ausschalten 
 endif
 
 if [Fan#State]=1
   GPIO,14,1                           // Lüfter einschalten
 endif 

 if [Fan#State]=0
   GPIO,14,0                           // Lüfter ausschalten
 endif 

endon